buildscript {
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
	}
}

version project.projectVersion
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java-library"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.internal.grails-plugin-publish"

sourceCompatibility = 17
targetCompatibility = 17

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    documentation
}

dependencies {
	compileOnly "org.grails:grails-boot"
	implementation "org.grails:grails-web"
	implementation "org.grails:grails-plugin-validation"
	implementation "org.grails:grails-datastore-core:2023.0.0-SNAPSHOT", {
        exclude group: "org.codehaus.groovy", module: "groovy"
    }
	implementation "org.grails:grails-datastore-gorm:2023.0.0-SNAPSHOT", {
        exclude group: "org.codehaus.groovy", module: "groovy"
    }
	implementation "org.grails:grails-web-gsp"
	implementation "org.grails:grails-scaffolding-core:$scaffoldingVersion"
	implementation "commons-lang:commons-lang:2.6"
	compileOnly "jakarta.servlet:jakarta.servlet-api:$servletApiVersion"
	
	testImplementation "org.grails:grails-test-support"

    documentation "org.fusesource.jansi:jansi:1.14"
	documentation "com.github.javaparser:javaparser-core:3.15.14"

	testImplementation 'javassist:javassist:3.12.0.GA'
	testImplementation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
	testImplementation "cglib:cglib-nodep:$cglibNodepVersion"
	testImplementation("org.jodd:jodd-wot:$joddWotVersion") {
		exclude module: 'slf4j-api'
		exclude module: 'asm'
	}
}

groovydoc.doFirst {
    classpath += (configurations.documentation + configurations.compileClasspath)
}

bootJar.enabled = false

jar {
    enabled = true
    archiveClassifier.set('')
}

grailsPublish {
	userOrg = "grails"
	githubSlug = project.githubSlug
	license {
		name = 'Apache-2.0'
	}
	title = project.title
	desc = project.projectDesc

	developers = [
			graemerocher: "Graeme Rocher",
			robfletcher: "Rob Fletcher",
			sbglasius: "SÃ¸ren Berg Glasius"
	]
}

